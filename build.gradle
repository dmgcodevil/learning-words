buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:1.0.0"
        classpath "jp.leafytree.gradle:gradle-android-scala-plugin:1.3.1"
    }
}

repositories {
    jcenter()
}

apply plugin: "com.android.application"
apply plugin: "jp.leafytree.android-scala"

android {
    compileSdkVersion "android-21"
    buildToolsVersion "21.1.1"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "com.test.learningwords.runner.MultiDexTestRunner"
    }

    sourceSets {
        main {
            scala {
                srcDir "src/main/scala"
            }
        }

        androidTest {
            scala {
                srcDir "src/androidTest/scala"
            }
        }
    }

    dexOptions {
        preDexLibraries false
        javaMaxHeapSize "2g"
    }
}

dependencies {
    compile "org.scala-lang:scala-library:2.11.4"
    compile 'org.apache.commons:commons-io:1.3.2'
    // ORM
    compile 'com.j256.ormlite:ormlite-android:4.48'

    compile 'org.apache.httpcomponents:httpclient-android:4.3.5'
    compile 'com.google.guava:guava:18.0'
    //compile 'com.loopj.android:android-async-http:1.4.6'
   // compile 'com.fasterxml.jackson.core:jackson-databind:2.4.2'
    compile "com.android.support:multidex:1.0.0"
}

tasks.withType(ScalaCompile) {
    scalaCompileOptions.deprecation = false
    scalaCompileOptions.additionalParameters = ["-feature"]
}

afterEvaluate {
    tasks.matching {
        it.name.startsWith("dex")
    }.each { dx ->
        dx.additionalParameters = (dx.additionalParameters ?: []) + [
            "--multi-dex",
            "--main-dex-list=$rootDir/main-dex-list.txt".toString(),
        ]
    }
}
